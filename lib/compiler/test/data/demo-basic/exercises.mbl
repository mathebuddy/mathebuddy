Exercises
#########


Multiple Choice
===============

EXERCISE My Multiple Choice Exercise  @ex:myMultiChoice
    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.

EXERCISE My Multiple Choice Exercise with static order  @ex:myMultiChoice
    ORDER=static
    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.


EXERCISE My dynamic Multiple Choice Exercise  @ex:myMultiChoice
    CODE
        let x/y/z/w = rand(10, 20);    % no pair of x, y, z, w is equal
        let c1 = x > w
        let c2 = y > w
        let c3 = z > w
    Choose the right answers:
    [:c1] $x > w$
    [:c2] $y > w$
    [:c3] $z > w$
    [x]   $1 > 0$    % statically true
    [ ]   $1 < 0$    % statically false


Single Choice
=============

EXERCISE My Single Choice Exercise  @ex:mySingleChoice
    Choose the right answer:
    (x) This answer is correct.
    ( ) This answer is incorrect.
    ( ) This answer is incorrect.


Randomized Exercises
====================

EXERCISE Addition  @ex:add
    CODE
        let x/y = rand(1, 5)
        let z = x + y
    Calculate $ x + y = $ #z.

EXERCISE Matrix Multiplication @ex:add-mul
    CODE
        let x:y = rand<2,2>(1, 5)
        let z = x * y
    Calculate $ x \cdot y = $ #z.
        

Mixed Exercises
===============

EXERCISE Mixed
    CODE
        let x/y = rand(1, 5)
        let z = x + y
    Calculate $ x + y = $ #z.

    Choose the right answers:
    [x] This answer is correct.
    [ ] This answer is incorrect.
    [x] This answer is correct.
    Choose the right answers:
    [x] True.
    [x] True.
    [ ] False.


Exercise with button answers
============================


EXERCISE Multiplication
    CHOICES=4
    CODE
        let x/y = rand(10, 20)
        let z = x * y
    $x \cdot y=$ #z


EXERCISE Simple Integration  @ex:intSimple
    CODE
        let F(x) = (1/3) * x^3 + 7*x
    Solve $\int (x^2+7) dx = $  #F,build_term  $+C$


Scoring
=======

EXERCISE Scoring example
    SCORES=5        % total score of the exercise (5/3 for fa; 10/3 for fb)
    CODE
        let a = rand(2, 4)
        let b = rand(5, 6)
        let fa = fac(a)
        let fb = fac(b)
    Calculate
    -) $ a! = $ #fa            % if scores are not given for a solution,
    -) $ b! = $ #fb,score=2    % it defaults to 1, i.e. fa has score 1


Gap Exercise
============

EXERCISE My Gap Exercise  @ex:myLabel
    SHOW_GAP_LENGTH=true
    SHOW_REQUIRED_LETTERS_ONLY=true
    Garfield is a #"cat". Rain is #"wet".


Arrangement Exercise
====================

EXERCISE Arrangement exercise
    ARRANGE=true
    ACCEPT_IMMEDIATELY=true
    CODE
        let n = rand(5,10)          % length
        let f = zeros<1,n>()        % row vector for the sequence
        f[1] = 1                    % f[0]=0, f[1]=1
        for (let i=2; i<n; i++) {   % calc sequence iteratively
            f[i] = f[i-2] + f[i-1]
        }
    Arrange the following numbers to get the first numbers of the Fibonacci sequence: #f


Timed Exercise
==============

EXERCISE Title @ex:label
    TIMER=3
    ACCELERATE=true
    STOP_AFTER_ERRORS=1
    CHOICES=4
    CODE
        let x:y = rand(20,50)
        let z = x + y
    Calculate $x+y=$ #z
